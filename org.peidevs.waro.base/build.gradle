
plugins {
    id 'java-library'
}

repositories {
    jcenter()
}

dependencies {
    // NOTE: automatic modules, referenced in module-info.java
    implementation 'org.springframework:spring-beans:4.1.5.RELEASE'
    implementation 'org.springframework:spring-context:4.1.5.RELEASE'
    implementation 'org.springframework:spring-core:4.1.5.RELEASE'

    testImplementation 'junit:junit:4.11'
}

ext.moduleName = 'org.peidevs.waro.base'

compileJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
            '--module-path', classpath.asPath, 
        ]
        classpath = files()  
    }
}

compileTestJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
            '--module-path', classpath.asPath, 
            '--add-modules', 'junit',  
            '--add-reads', "$moduleName=junit", 
            '--patch-module', "$moduleName=" + files(sourceSets.test.java.srcDirs).asPath, 
        ]
        classpath = files()
    }
}

/*
jar {
    doLast {
        ant.copy(file: "${buildDir}/libs/org.peidevs.waro.base.jar", todir: "${projectDir}/../mlib")
    }
}
*/
