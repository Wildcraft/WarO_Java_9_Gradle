
// from https://github.com/gradle-guides/building-java-9-modules
// build tickle

subprojects {
    afterEvaluate {
        repositories {
            jcenter()
        }

        compileJava {
            inputs.property("moduleName", moduleName)
            doFirst {
                options.compilerArgs = [
                    '--module-path', classpath.asPath, 
                ]
                classpath = files()  
            }
        }

        compileTestJava {
            inputs.property("moduleName", moduleName)
            doFirst {
                options.compilerArgs = [
                    '--module-path', classpath.asPath, 
                    '--add-modules', 'junit',  
                    '--add-reads', "$moduleName=junit", 
                    '--patch-module', "$moduleName=" + files(sourceSets.test.java.srcDirs).asPath, 
                ]
                classpath = files()
            }
        }
    }
}

