
plugins {
    id 'java-library'
}

repositories {
    jcenter()
}

dependencies {
    implementation project(":org.peidevs.waro.base")

    // NOTE: automatic module, referenced in module-info.java
    implementation 'com.google.guava:guava:18.0'

    testImplementation 'junit:junit:4.11'
}

ext.moduleName = 'org.peidevs.waro.function'

compileJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
            '--module-path', classpath.asPath,
        ]
        classpath = files()  
    }
}

compileTestJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
            '--module-path', classpath.asPath,
            '--add-modules', 'junit',  
            '--add-reads', "$moduleName=junit", 
            '--patch-module', "$moduleName=" + files(sourceSets.test.java.srcDirs).asPath, 
        ]
        classpath = files()
    }
}
